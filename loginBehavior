constant kTextColorDark = "40,40,40"
constant kTextColorLight = "240,240,240"
constant kTextColorDisabledDark = "180,180,180"
constant kTextColorDisabledLight = "230,230,230"
constant kBorderColor = "214,214,214"
constant kHiliteColor = "66,148,248"
constant kNotHiliteColor = "136,136,136"
constant kColorPressed = "0,0,139"
constant kDisabledStyle = "italic"
constant kEnabledStyle = "plain"

# set text sizes and colors
command formatFieldText pSize, pTextColor, pBorderColor
    if pBorderColor is empty then put kBorderColor into pBorderColor
    if pTextColor is empty then put kTextColorDark into pTextColor
    // fields
    set the textColor of field "username" of me to pTextColor
    set the textSize of field "username" of me to pSize
    set the borderColor of field "username" of me to pBorderColor
    set the textColor of field "password" of me to pTextColor
    set the borderColor of field "password" of me to pBorderColor
    set the textSize of field "password" of me to pSize
    set the textSize of field "signInLabel" of me to pSize + 6
    set the textSIze of field "resetPassword" of me to pSize - 6
    //widgets
    set the width of widget "username" of me to pSize
    set the height of widget "username" of me to pSize
    set the width of widget "password" of me to pSize
    set the height of widget "password" of me to pSize
    set the width of widget "showPassword" of me to pSize
    set the height of widget "showPassword" of me to pSize
    set the foregroundColor of widget "username" to pBorderColor
    set the foregroundColor of widget "password" to pBorderColor
    set the foregroundColor of widget "showPassword" to pBorderColor
    // buttons
    set the textsize of button "Login" of me to pSize
    // create image to obscure password
    createCircle pSize, pTextColor, pBorderColor
    // center text vertically
    centerAllFieldsVertically
end formatFieldText

private command centerAllFieldsVertically
    local tTextHeight, tFieldY, tFormatRect, tFormatheight, tFormatHalfHeight, tCurrFormatTop, tCenterField_To_TopTextDiff
    repeat with x = 1 to the number of fields of me
        if the short name of field x of me is not "resetPassword" then
            put the effective textHeight of field x of me into tTextHeight
            put item 2 of the loc of field x of me into tfieldY
            put the formattedRect of line 1 to - 1 of field x of me into tFormatRect
            put item 4 of tFormatRect - item 2 of tFormatRect into tFormatHeight
            put tFormatHeight div 2 into tFormatHalfHeight
            put item 2 of tFormatRect into tCurrFormatTop
            put tfieldY - tCurrFormatTop into tCenterField_To_TopTextDiff
            set the topMargin of field x of me to the topMargin of field x of me + tCenterField_To_TopTextDiff - tFormatHalfHeight
        end if
    end repeat
    send "exitField" to field "username"
    send "exitField" to field "password"
end centerAllFieldsVertically

private command createCircle pSize, pTextColor, pBorderColor
    local tPic
    if there is a graphic "blackCircle" of me then delete graphic "blackCircle" of me
    if there is an image "blackCircle" of me then delete image "blackCircle" of me
    if pTextColor is empty then put kTextColorDark into pTextColor
    if pBorderColor is empty then put kBorderColor into pBorderColor
    
    create invisible graphic "blackCircle" in me
    set the style of grc "blackCircle" of me to "oval"
    set the opaque of grc "blackCircle" of me to true
    set the backgroundColor of grc "blackCircle" of me to pTextColor
    set the foregroundColor of grc "blackCircle" of me to pBorderColor
    set the height of grc "blackCircle" of me to pSize
    set the width of grc "blackCircle" of me to pSIze
    set the loc of grc "blackCircle" of me to the left of me + 20, item 2 of the loc of me
    local tImage
    export snapshot from grc "blackCircle" of me to tImage as PNG 
    create invisible image "blackCircle" in me
    put tImage into image "blackCircle" of me
    set the loc of image "blackCircle" of me to the loc of me
    delete graphic "blackCircle" of me
end createCircle

private function fieldTextSize
    local tRect, tFHeight, tTargetSize
    put the rect of field 1 of me into tRect
    put the height of field "username" of me into tFHeight
    put round (the height of field "username" * 0.5) into tTargetSize
    repeat with x = 6 to 50
        if x = tTargetSize then exit repeat
    end repeat
    if x is empty then put 13 into x
    return x
end fieldTextSize

on openControl
    initWidget
    focus on nothing
end openControl

command initWidget
    put empty into field "username" of me
    send "closefield" to field "username" of me
    send "exitField" to field "username" of me
    put empty into field "password" of me
    send "closefield" to field "password" of me
    send "exitField" to field "password" of me
    set the hilite of widget "showPassword" of me to false
    set the showPassword of field "password" of me to false
    set the iconPresetName of widget "showPassword" of me to "eye close"
    disable button "login"
    focus on nothing
end initWidget

on resizeControl
    local tRect, tWidth, tHeight
    put the rect of me into tRect
    add 4 to item 1 of tRect
    add 4 to item 2 of tRect
    subtract 4 from item 3 of tRect
    subtract 4 from item 4 of tRect
    set the rect of grc "background" of me to tRect
    
    put 0.69 * the width of grc "background" of me into tWidth
    put 0.08 * the height of grc "background" of me into tHeight
    // signInLabel
    set the width of field "signInLabel" of me to tWidth
    set the height of field "signInLabel" of me to tHeight
    set the loc of field "signInLabel" of me to the loc of grc "background" of me
    set the top of field "signInLabel" of me to the top of grc "background" of me + tHeight
    // field username
    set the width of field "username" of me to tWidth
    set the height of field "username" of me to tHeight
    set the loc of field "username" of me to the loc of grc "background" of me
    set the top of field "username" of me to the bottom of field "signInLabel" of me + 10
    // field password
    set the width of field "password" of me to tWidth
    set the height of field "password" of me to tHeight
    set the loc of field "password" of me to the loc of grc "background" of me
    set the top of field "password" of me to the bottom of field "username" of me + tHeight
    // widget username
    set the loc of widget "username" of me to the loc of field "username" of me
    set the right of widget "username" of me to the left of field "username" of me - 6
    // widget password
    set the loc of widget "password" of me to the loc of field "password" of me
    set the right of widget "password" of me to the left of field "password" of me - 6
    // widget showPassword
    set the loc of widget "showPassword" of me to the loc of field "password" of me
    set the left of widget "showPassword"  of me to the right of field "password" of me + 6
    // field resetPassword
    set the top of field "resetPassword" of me to the bottom of field "password" of me + 2
    set the right of field "resetPassword" of me to the right of field "password" of me
    // button login
    set the height of button "Login" of me to tHeight + 4
    set the width of button "Login" of me to 0.5 * tWidth
    set the loc of button "Login" of me to the loc of grc "background" of me
    set the bottom of button "Login" to the bottom of grc "background" of me - tHeight - 10
    // button loginBehavior (invisible)
    set the loc of button "loginBehavior" of me to the loc of grc "background" of me
    
    formatFieldText fieldTextSize(), empty, empty
    centerAllFieldsVertically
end resizeControl

on mouseDown
    switch the short name of the target
        case "resetPassword"
            set the textColor of field "resetPassword" of me to kColorPressed
            break
            
    end switch
    
    pass mouseDown
end mouseDown

on mouseRelease
    switch the short name of the target
        case "resetPassword"
            set the textColor of field "resetPassword" of me to kHiliteColor
            break
            
    end switch
end mouseRelease

on mouseUp
    switch the short name of the target
        case "Login"
            loginWithUsernameAndPassword field "username" of me, field "password" of me
            break
            
        case "showPassword"
            if the mouseLoc is not within the rect of widget "showPassword" of me then pass mouseUp
            set the hilite of widget "showPassword" of me to not the hilite of widget "showPassword" of me
            if the hilite of widget "showPassword" of me  then 
                set the iconPresetName of widget "showPassword" me to "eye open"
            else
                set the iconPresetName of widget "showPassword" me to "eye close"
            end if
            set the showPassword of field "password" of me to the hilite of widget "showPassword" of me
            dispatch "showHideText" to field "password" of me
            break
            
        case "resetPassword"
            set the textColor of field "resetPassword" of me to kHiliteColor
            resetPasswordForUsername field "username"
            break
    end switch
    
    pass mouseUp
end mouseUp


# return dark textColor for light backgrounds & vice versa
private function textColorForBackground pColor, pDisabled
    local hsp, r, g, b
    if char 1 of pColor is "#" and length(pColor) = 7 then // hex
        put baseConvert(char 2 to 3 of pColor, 16, 10) into r
        put baseConvert(char 4 to 5 of pColor, 16, 10) into g
        put baseConvert(char 6 to 7 of pColor, 16, 10) into b
    else if length(pColor) = 6 then
        put baseConvert(char 1 to 2 of pColor, 16, 10) into r
        put baseConvert(char 3 to 4 of pColor, 16, 10) into g
        put baseConvert(char 5 to 5 of pColor, 16, 10) into b
    else if pColor contains ","  then// rbg
        put item 1 of pColor into r
        put item 2 of pColor into g
        put item 3 of pColor into b
    end if
    put sqrt(0.299 * (r * r) + 0.587 * (g * g) + 0.114 * (b * b)) into hsp
    if hsp > 127.5 then
        if pDisabled then
            return kTextColorDisabledDark
        else
            return kTextColorDark
        end if
    else
        if pDisabled then
            return kTextColorDisabledLight
        else
            return kTextColorLight
        end if
    end if
end textColorForBackground

################################
##   PLACEHOLDERTEXT FUNCTIONS  ##
################################

local sFieldID // the long id of the current field, set on openField

function textFieldIsEmpty pID
    if pID is empty then exit to top
    get the text of pID
    if it = "" or it = the uPlaceholderText of pID then
        return true
    else
        return false
    end if
end textFieldIsEmpty

on setTextStyle pMode ## pMode: open - for when entering field | close - for when leaving field
    if sFieldID is empty then exit to top
    if textFieldIsEmpty(sFieldID) then
        if pMode = "open" then
            set the text of sFieldID to empty
            set the textColor of sFieldID to textColorForBackground(the backgroundColor of grc "background" of me, false)
            set the textStyle of sFieldID to kEnabledStyle
        else 
            set the text of sFieldID to the uPlaceholderText of sFieldID
            set the textColor of sFieldID to textColorForBackground(the backgroundColor of grc "background" of me, true)
            set the textStyle of sFieldID to kDisabledStyle
        end if      
    else //field not empty or placeholder text
        set the textColor of sFieldID to textColorForBackground(the backgroundColor of grc "background" of me, false)
        set the textStyle of sFieldID to kEnabledStyle
    end if
end setTextStyle

on openField
    if word 1 of the long id of the target is "field"  then 
        put the long id of the target into sFieldID
    else
        put empty into sFieldID
    end if
    setTextStyle "open"
end openField

on closeField
    setTextStyle "close"
end closeField

on exitField
    setTextStyle "close"
end exitField

on textChanged
    if sFieldID is empty then exit to top
    if field "username" of me is empty or field "password" of me is empty then
        set the enabled of button "login" of me to false
    else
        set the enabled of button "login" of me to true
    end if
   if the showPassword of sFieldID is not empty then showHideText
end textChanged


################################
##   BULLETPOINT TEXT FUNCTIONS   ##
################################
command showHideText
   lock screen
   if the showPassword of sFieldID is false then
      repeat with x = 1 to the length of sFieldID
         set the imageSource of character x of sFieldID to the id of image "blackCircle" of me
      end repeat
   else
      repeat with x = 1 to the length of sFieldID
         set the imageSource of character x of sFieldID to empty
      end repeat
   end if
end showHideText

on copyKey // suspend development tools to use this in iDE
   --   trap copy
end copyKey

on cutKey // suspend development tools to use this in iDE
   --trap cut
end cutKey

on pasteKey // suspend development tools to use this in iDE
   local temp
   put the clipboardData["text"] into temp
   set the text of sFieldID to temp
   showHideText
   select after sFieldID
end pasteKey

on rawKeyDown pKeyCode // trap return/enter in fields
    switch pKeyCode
        case 65293 // return
        case 65421 // enter
            if not textFieldIsEmpty(the long id of field "username" of me) and not textFieldIsEmpty(the long id of field "password" of me) then
                click at the loc of button "Login" of me
            else
                if "username" is in the long name of sFieldID then 
                    select after field "password" of me
                else
                    select after field "username" of me
                end if
            end if
            break
        case 65307 // escape
            send "exitField" to field "username" of me
            send "closeField" to field "username" of me
            send "exitField" to field "password" of me
            send "closeField" to field "password" of me
            focus on nothing
            break
        default 
            pass rawKeyDown
    end switch
end rawKeyDown
